apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'vertx'

repositories {
	mavenCentral()
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integration-test/java')
        groovy.srcDir file('src/integration-test/groovy')
        resources.srcDir file('src/integration-test/resources')
    }
}

dependencies {
	groovy 'org.codehaus.groovy:groovy:1.8.6'

	testCompile 'org.spockframework:spock-core:0.6-groovy-1.8'

    integrationTestCompile sourceSets.main.output
    integrationTestCompile configurations.testCompile
    integrationTestCompile sourceSets.test.output

    integrationTestRuntime configurations.testRuntime
}

task integrationTest(type: Test) {
	description = 'Runs the integration tests.'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest

vertxDeploy {
	dependsOn processIntegrationTestResources
	classpath(new File('build/resources/integrationTest'))
	main = 'Server.groovy'
}

gradle.addListener new VertxLifecycleListener()

class VertxLifecycleListener implements TaskExecutionListener {
	@Override
	void beforeExecute(Task task) {
		if (task.name == 'integrationTest') {
			task.project.tasks.vertxStart.execute()
			task.project.tasks.vertxDeploy.execute()
		}
	}

	@Override
	void afterExecute(Task task, TaskState taskState) {
		if (task.name == 'integrationTest') {
			task.project.tasks.vertxStop.execute()
		}
	}
}